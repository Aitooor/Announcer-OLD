buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'kr.entree.spigradle.bungee' version '2.4.3'
    id 'io.freefair.lombok' version '8.0.1'
    id 'java'
    id 'jacoco'
    id 'idea'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 8
targetCompatibility = 8

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

bungee {
    name project.property("pluginName")
    description 'Announcer plugin for Nasgar Network'
    version project.property("version")
    author project.property("author")
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/public/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url = 'https://repo.unnamed.team/repository/unnamed-public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:${mcVersion}-R0.1-SNAPSHOT"
    compileOnly "net.md-5:bungeecord-api:${bungeeVersion}-R0.1-SNAPSHOT"

    // Annotation Command Framework: https://github.com/aikar/commands
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation "co.aikar:acf-bungee:0.5.1-SNAPSHOT"

    implementation('net.silthus.configlib:configlib-bukkit:2.6.0') {
        exclude group: 'org.spigotmc', module: 'spigot-api'
    }
    implementation('net.silthus.configlib:configlib-bungee:2.6.0') {
        exclude group: 'net.md-5.bungee', module: 'bungeecord-api'
    }

    implementation 'me.yushust.message:core:7.1.0'
    implementation 'me.yushust.message:sourcetype-bukkit-yml:7.1.0'
    implementation 'me.yushust.message:sourcetype-bungee-yml:7.1.0'

    implementation 'me.clip:placeholderapi:2.11.2'
}

shadowJar {
    minimize()
    classifier = ''
    dependencies {
        include(dependency('co.aikar:acf-bungee:'))
        include(dependency('co.aikar:acf-paper:'))
        include(dependency('net.silthus.configlib:configlib-bukkit:'))
        include(dependency('net.silthus.configlib:configlib-bungee:'))
        include(dependency('me.yushust.message:core:'))
        include(dependency('me.yushust.message:sourcetype-bukkit-yml:'))
        include(dependency('me.yushust.message:sourcetype-bungee-yml:'))
    }
    relocate 'co.aikar.commands', "${packageName}.acf"
    relocate 'co.aikar.locales', "${packageName}.locales"
    relocate 'de.exlll.configlib', "${packageName}.configlib"
    relocate 'me.yushust.message', "${packageName}.nmessage"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
